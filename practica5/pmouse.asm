;------------------------------------------------
;MUESTRA LAS COORDENADAS DE LA POSICI?N ACTUAL DEL PUNTERO DEL MOUSE
;USO DE INT 21, SERVICIO 40
;------------------------------------------------

.MODEL SMALL
.STACK 100H
.DATA
XB DW 00
YB DW 00
VALASC DW 00
MEN1 DB 'Mouse no disponible...$'
DESPDATO LABEL BYTE
XMEN DB 'x= '
XASCII DW ?
YMEN DB ' y= '
YASCII DW ?

YXI DW 00
YXF DW 00
FPP DB 00

.CODE
MAIN PROC FAR
;PROTOCOLO
PUSH DS
SUB AX,AX
PUSH AX
MOV AX,@DATA
MOV DS,AX
MOV ES,AX
;FIN PROTOCOLO

CALL INITTEXT
MOV FPP,3EH
MOV YXI,00H
MOV YXF,184FH
CALL TEXTBACKGROUND
CALL I_MOUSE
CMP AX,00
JE ERROR
CALL M_MOUSE
MOV YXI,1843H
OTRO: CALL P_MOUSE ; SERVICIO 03
CMP BX,01
JE EXIT
CALL GOTOXY
MOV AX,XB
CALL CONV
MOV AX,VALASC
MOV XASCII,AX
MOV AX,YB
CALL CONV
MOV AX,VALASC
MOV YASCII,AX
CALL DESPLIEGA ;SERVICIO 40 DE LA INT 21
JMP OTRO
ERROR: LEA DX,MEN1
CALL PRINTF
EXIT: CALL F_MOUSE
MOV FPP,07h
MOV YXI,00H
MOV YXF,184FH
CALL TEXTBACKGROUND
MOV AX,4C00H
INT 21H
RET
MAIN ENDP

I_MOUSE PROC NEAR
MOV AX,00
INT 33H
RET
I_MOUSE ENDP

M_MOUSE PROC NEAR
PUSH AX
MOV AX,01H
INT 33H
POP AX
RET
M_MOUSE ENDP

C_MOUSE PROC NEAR
PUSH AX
MOV AX,04H
INT 33H
POP AX
RET
C_MOUSE ENDP

F_MOUSE PROC NEAR
PUSH AX
MOV AX,02H
INT 33H
POP AX
RET
F_MOUSE ENDP

C_CURSOR PROC NEAR
MOV AH,02
MOV BH,00
MOV DH,00
MOV DL,25
INT 10H
RET
C_CURSOR ENDP

P_MOUSE PROC NEAR
;La subrutina devuelve XB,YB

SAL3: MOV AX,03 
INT 33H     
CMP BX,01   
JE SAL1
MOV AX,CX 
MOV CL,3
SHR AX,CL 
SHR DX,CL 
MOV CX,AX 
CMP CX,XB 
JNE SAL2 
CMP DX,YB
JE SAL3 
SAL2: MOV XB,CX 
MOV YB,DX
SAL1: RET
P_MOUSE ENDP

CONV PROC NEAR
MOV VALASC,2020H 
MOV CX,10 
LEA SI,VALASC+1
CMP AX,CX 
JB C1 
DIV CL 
OR AH,30H 
MOV [SI],AH
DEC SI
C1: OR AL,30H 
MOV [SI],AL
RET
CONV ENDP

DESPLIEGA PROC NEAR
; Servicio 40h :manejador de archivo para despliegue en pantalla
MOV AH,40H
MOV BX,01 
MOV CX,11 
LEA DX,DESPDATO 
INT 21H
RET
DESPLIEGA ENDP

INITTEXT PROC NEAR
PUSH AX
MOV AH,00H
MOV AL,03H
INT 10H
POP AX
RET
INITTEXT ENDP

PRINTF PROC NEAR
PUSH AX
MOV AH,09H
INT 21H
POP AX
RET
PRINTF ENDP

TEXTBACKGROUND PROC NEAR
PUSH AX
PUSH BX
PUSH CX
PUSH DX
MOV AX,0600H
MOV BH,FPP
MOV CX,YXI
MOV DX,YXF
INT 10H
POP DX
POP CX
POP BX
POP AX
RET
TEXTBACKGROUND ENDP

GOTOXY PROC NEAR
PUSH AX
PUSH BX
PUSH DX
MOV AH,02H
MOV BH,00H
MOV DX,YXI
INT 10H
POP DX
POP BX
POP AX
RET
GOTOXY ENDP
END MAIN
